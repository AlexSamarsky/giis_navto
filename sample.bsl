// Открывает билет/квитанцию по типу квитанции в браузере
// Параметры:
//   Билет - Строка - Номер билета/квитанции в гиис. 
//
Процедура ОткрытьКвитанциюВБраузере(Билет) Экспорт
	
	Программа = МаркировкаПовтИсп.ПрограммаДоступаКГИИСДМДК();
	
	СсылкаКвитанции = МаркировкаВызовСервера.ПолучитьСсылкуПереходаВБраузере(Билет);
	
	Если Программа = ПредопределенноеЗначение("Перечисление.ВидыПрограммДоступаКГИИСДМДК.Авто")
		ИЛИ НЕ ОбщегоНазначенияКлиентСервер.ЭтоWindowsКлиент() Тогда
		
		ЗапуститьПриложение(СсылкаКвитанции);
		
	ИначеЕсли Программа = ПредопределенноеЗначение("Перечисление.ВидыПрограммДоступаКГИИСДМДК.ChromiumGOST") Тогда
		
		ДанныеЗапуска = Новый Массив();
		ДанныеЗапуска.Добавить(
			ДомашнийКаталог()
			+ "\AppData\Local\Chromium\Application\chrome.exe"
		);
		ДанныеЗапуска.Добавить(СсылкаКвитанции);
		
		ОбщегоНазначенияКлиентСервер.ЗапуститьПрограмму(
			ДанныеЗапуска,
			ОбщегоНазначенияКлиентСервер.ПараметрыЗапускаПрограммы()
		);
		
	ИначеЕсли Программа = ПредопределенноеЗначение("Перечисление.ВидыПрограммДоступаКГИИСДМДК.Yandex") Тогда
		
		ДанныеЗапуска = Новый Массив();
		ДанныеЗапуска.Добавить(
			ДомашнийКаталог()
			+ "\AppData\Local\Yandex\YandexBrowser\Application\browser.exe"
		);
		ДанныеЗапуска.Добавить(СсылкаКвитанции);
		
		ОбщегоНазначенияКлиентСервер.ЗапуститьПрограмму(
			ДанныеЗапуска,
			ОбщегоНазначенияКлиентСервер.ПараметрыЗапускаПрограммы()
		);
	Иначе
		ВызватьИсключение НСтр("ru='Неожиданный тип программы доступа к ГИИС ДМДК'");
	КонецЕсли;
	
КонецПроцедуры

// Определяет домашний каталог пользователь для windows-клиента
// Возвращаемое значение:
//   Строка - полный путь до домашнего каталога пользователя
Функция ДомашнийКаталог() Экспорт
	
	Если НЕ ОбщегоНазначенияКлиентСервер.ЭтоWindowsКлиент() Тогда
		ВызватьИсключение НСтр("ru='Поддерживается только windows клиент'");
	КонецЕсли;
	
	Параметры = ОбщегоНазначенияКлиентСервер.ПараметрыЗапускаПрограммы();
	Параметры.ПолучитьПотокВывода = Истина;
	Параметры.ПолучитьПотокОшибок = Истина;
	Параметры.ДождатьсяЗавершения = Истина;
	
	пр = Новый Массив();
	пр.Добавить("echo");
	пр.Добавить("%userprofile%");
	
	Результат = ОбщегоНазначенияКлиентСервер.ЗапуститьПрограмму(
		пр,
		Параметры
	);
	
	Если Результат.КодВозврата <> 0 Тогда
		ВызватьИсключение Результат.ПотокОшибок;
	КонецЕсли;
	
	Возврат СокрЛП(Результат.ПотокВывода);
	
КонецФункции

// По типам билетов генерирует ссылки для переход в браузере
// Параметры:
//   Билеты - Массив из СправочникСсылка.БилетыКвитанции - билеты, для которых нужно сгенерировать ссылки
// Возвращаемое значение:
//   Массив Из Строка - ссылки к отрытию в браузере
Функция ПолучитьСсылкиПереходаВБраузере(Билеты) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	БилетыКвитанции.Тип КАК Тип,
	|	НомераБилетовКвитанций.НомерВГИИС КАК НомерВГИИС
	|ИЗ
	|	РегистрСведений.НомераБилетовКвитанций КАК НомераБилетовКвитанций
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.БилетыКвитанции КАК БилетыКвитанции
	|		ПО НомераБилетовКвитанций.БилетКвитанция = БилетыКвитанции.Ссылка
	|ГДЕ
	|	НомераБилетовКвитанций.НомерВГИИС <> """"
	|	И БилетыКвитанции.Ссылка В (&Ссылка)");
	Запрос.УстановитьПараметр("Ссылка", Билеты);
	
	СсылкиГИИС = Новый Массив();
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СоответствиеСсылок = Новый Соответствие();
	
	СоответствиеСсылок.Вставить(Перечисления.ТипКвитанции.DT_RECEIPT_FOR_BUYINGUP, "/individuals/buying-up-receipts?amsearch=");
	СоответствиеСсылок.Вставить(Перечисления.ТипКвитанции.DT_PLEDGE_TICKET, "/individuals/pawnshop?amsearch=");
	СоответствиеСсылок.Вставить(Перечисления.ТипКвитанции.DT_RECEIPT_FOR_COMMISSION_TRAIDING, "/individuals/commission-trading?amsearch=");
	
	Если Константы.ИспользоватьТестовыйКонтур.Получить() Тогда
		Если Константы.РежимТестовогоКонтура.Получить() = Перечисления.ТестовыеКонтуры.ТестовыйКонтур Тогда
			Сервер = "https://dmdk.goznak.ru";
		Иначе
			Сервер = "https://testlk.dmdk.ru";
		КонецЕсли;
	Иначе
		Сервер = "https://lk.dmdk.ru";;
	КонецЕсли;
	
	Пока Выборка.Следующий() Цикл
		СсылкиГИИС.Добавить(Сервер + СоответствиеСсылок[Выборка.ТИП] + Выборка.НомерВГИИС);
	КонецЦикла;
	
	Возврат СсылкиГИИС;
	
КонецФункции
